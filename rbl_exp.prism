mdp

const reward_unseen_speed_1;
const reward_unseen_speed_2;
const reward_unseen_speed_3;
const reward_unseen_speed_4;
const reward_unseen_speed_5;

const ofc_to_drn_1;
const ofc_to_drn_2;
const ofc_to_drn_3;
const ofc_to_drn_4;
const ofc_to_drn_5;

const hc_to_ofc_1;
const hc_to_ofc_2;
const hc_to_ofc_3;
const hc_to_ofc_4;
const hc_to_ofc_5;


const reward_reset_step;

const drn_reward_wait_threshold;



const pf_length; //length of traversable area in a placefield
const delay; //number of steps between agent entering a placefield and reward spawning

//random speeds for traversing placefield before OFC-DRN connection established
const mpfc_random_speed_lo; 
const mpfc_random_speed_med;
const mpfc_random_speed_hi;


const reward_seen_speed = 40; //speed after reward seen (vis2 = 1)

const reward_pos_in_pf; //must be significantly less than pf_length

global reward_collected : bool init false;
global waiting : bool init false; // agent has slowed in front of reard location
global in_reward_pf : bool init false;



module agentMotor

//am_state=0: random movement to pf
//am_state=1: agent is in pf; if drn > drn_move_threshold AND pf corresponds with learned reward location, goto am_state = 3, else, goto am_state=2
//am_state=2: agent is advancing at random speed. If agent reaches reward landmark, goto am_state = todo (end)
//am_state=3: agent is advancing using speed determined by DRN output,If agent reaches reward landmark, goto am_state = todo (end)
//am_state=4: check if agent has received reward. output OFCtoDRN as necessary
//am_state=5: signal OFC 
//am_state=6: reset to am_state=0;

pos : [0..100] init 0;//0 is starting position //TODO rename pf
inpf_pos : [0..pf_length];
ss : [0..200] init 0; //reward location reset counter
am_state : [0..10] init 0; //state counter for this module

vis1 : [0..1] init 0; // reward landmark seen
vis2 : [0..1] init 0; // reward seen

reward_pos : [0..100] init 1; //placefield containing reward


[] (am_state = 0 )-> //move randomly to a placefield  
1/3 : (pos'=1) & (am_state' = 1)  + 1/3 : (pos'=2) & (am_state' = 1)  + 1/3 : (pos'=3) &  (am_state' = 1);



[] am_state=1 & ((HC=1 & OFC1toDRN>drn_reward_wait_threshold) 
| (HC=2 & OFC2toDRN>drn_reward_wait_threshold) 
| (HC=3 & OFC3toDRN>drn_reward_wait_threshold)) -> //if agent in same pf as learned reward location, goto am_state=3
	(am_state'=3) & (waiting'=true) & (vis1'=1) ;

[] am_state=1 & (!(HC=1 & OFC1toDRN>drn_reward_wait_threshold) 
| !(HC=2 & OFC2toDRN>drn_reward_wait_threshold) 
| !(HC=3 & OFC3toDRN>drn_reward_wait_threshold)) -> 
	(am_state'=2)  & (waiting'=true) & (vis1'=1) ;

[agent_move] am_state=2 & inpf_pos + mpfc_random_speed_hi < pf_length -> //move forward using random speed 
	 1/3 : (inpf_pos' = inpf_pos + mpfc_random_speed_lo) +
	 1/3 : (inpf_pos' = inpf_pos + mpfc_random_speed_med) +
	 1/3 : (inpf_pos' = inpf_pos + mpfc_random_speed_hi); 



//agent advances each step by speed deterimed from DRN lookup table
[agent_move] am_state=3 & HC=1 & vis2=0 & inpf_pos + reward_unseen_speed_pf_1 < pf_length-> (inpf_pos'=inpf_pos+reward_unseen_speed_pf_1);

[agent_move] am_state=3 & HC=2 & vis2=0 & inpf_pos + reward_unseen_speed_pf_2 < pf_length  -> (inpf_pos'=inpf_pos+reward_unseen_speed_pf_2);

[agent_move] am_state=3 & HC=3 & vis2=0 & inpf_pos + reward_unseen_speed_pf_3 < pf_length  -> (inpf_pos'=inpf_pos+reward_unseen_speed_pf_3);

[agent_move] (am_state=2 | am_state=3) & reward_present=true -> (vis2'=1); 

[agent_move] am_state=3 & vis2=1 & inpf_pos + reward_seen_speed < pf_length  -> (inpf_pos'=inpf_pos+reward_seen_speed);

[agent_move] (am_state=2 | am_state = 3) & inpf_pos>= reward_pos_in_pf ->
	(am_state'=4);

[] am_state=4 & HC = reward_pos & reward_present = true ->  //agent is in the placefield with the reward and the reward has appeared, so the agent collects the reward
	(reward_collected'=true) & (am_state'=5) & (waiting'=false) & (inpf_pos'=0);

[] am_state=4 & reward_present = true & HC != reward_pos -> //fake reward has appeared as agent is not in the correct placefield, so no reward gained.
	(reward_collected'=false) & (am_state'=5) & (waiting'=false) & (inpf_pos'=0);

[] am_state=4 & reward_present = false & HC = reward_pos ->//reward has not appeared and agent is in the correct placefield, so no reward gained
	(reward_collected'=false) & (am_state'=5) & (waiting'=false) & (inpf_pos'=0);

[] am_state=4 & reward_present = false & HC != reward_pos -> //reward has not appeared and agent is not in the correct placefield, so no reward gained
	(reward_collected'=false) & (am_state'=5) & (waiting'=false) & (inpf_pos'=0);

[signalOFC] (am_state = 5 & ss!=reward_reset_step) & ss + 1 < 100 ->(ss' = ss + 1) & (am_state' = 6); //send signal to OFC and reset am_state

[reset] (am_state = 5 & ss=reward_reset_step) ->   //reset reward location on reset_reward_step
	1/3 :(pos'=0) & (am_state' = 6) & (reward_pos' = 1) & (ss' = 0) + 
	1/3 :(pos'=0) & (am_state' = 6) & (reward_pos' = 2) & (ss' = 0) + 
	1/3 :(pos'=0) & (am_state' = 6) & (reward_pos' = 3) & (ss' = 0);

[] am_state = 6->(pos' = 0) & (am_state'=0) & (reward_collected'=false) & (in_reward_pf'=false) & (vis1'=0) & (vis2'=0);//reset variables for each run





endmodule




module OFC
OFC1toDRNkey : [0..100] init 0;
OFC2toDRNkey : [0..100] init 0;
OFC3toDRNkey : [0..100] init 0;

HCtoOFC1key : [0..100] init 0;
HCtoOFC2key : [0..100] init 0;
HCtoOFC3key : [0..100] init 0;


//if DRN to OFC connection is high, increase HC to relevant OFC connection strength and increase OFC to DRN connection
[signalOFC] (DRNtoOFC = 3 & HCtoOFC1key < 2 & OFC1toDRNkey < 2 & HC=1) -> 
 (HCtoOFC1key' = HCtoOFC1key + 1) & (OFC1toDRNkey' = OFC1toDRNkey + 1);

[signalOFC] (DRNtoOFC = 3 & HCtoOFC2key < 2 & OFC2toDRNkey < 2 & HC=2) ->
 (HCtoOFC2key' = HCtoOFC2key + 1) & (OFC2toDRNkey' = OFC2toDRNkey + 1);

[signalOFC] (DRNtoOFC = 3 & HCtoOFC3key < 2 & OFC3toDRNkey < 2 & HC=3) ->
 (HCtoOFC3key' = HCtoOFC3key + 1) & (OFC3toDRNkey' = OFC3toDRNkey + 1); 



//upper limit connection strengths
[signalOFC] (DRNtoOFC = 3 & HCtoOFC1key = 4 & HC=1) -> 
	(HCtoOFC1key' = 4) & (OFC1toDRNkey'=4);
[signalOFC] (DRNtoOFC = 3 & HCtoOFC2key = 4 & HC=2) -> 
	(HCtoOFC2key' = 4) & (OFC2toDRNkey'=4);
[signalOFC] (DRNtoOFC = 3 & HCtoOFC3key = 4 & HC=3) -> 
	(HCtoOFC3key' = 4) & (OFC3toDRNkey'=4);


//if DRN to OFC connection is low, decrease HC to relevant OFC connection strength
[signalOFC] (DRNtoOFC = 1 & HCtoOFC1key > 0 & OFC1toDRNkey > 0 & HC=1) ->
 (HCtoOFC1key' = HCtoOFC1key - 1) & (OFC1toDRNkey' = OFC1toDRNkey - 1);

[signalOFC] (DRNtoOFC = 1 & HCtoOFC2key > 0 & OFC2toDRNkey > 0  & HC=2) ->
 (HCtoOFC2key' = HCtoOFC2key - 1) & (OFC2toDRNkey' = OFC2toDRNkey - 1);

[signalOFC] (DRNtoOFC = 1 & HCtoOFC3key > 0 & OFC3toDRNkey > 0  & HC=3) ->
 (HCtoOFC3key' = HCtoOFC3key - 1) & (OFC3toDRNkey' = OFC3toDRNkey - 1);


// lower limit connection strengths
[signalOFC] (DRNtoOFC = 1 & HCtoOFC1key = 0 & HC=1) -> (HCtoOFC1key' = 0) & (OFC1toDRNkey'=0);
[signalOFC] (DRNtoOFC = 1 & HCtoOFC2key = 0 & HC=2) -> (HCtoOFC2key' = 0) & (OFC2toDRNkey'=0);
[signalOFC] (DRNtoOFC = 1 & HCtoOFC3key = 0 & HC=3) -> (HCtoOFC3key' = 0) & (OFC3toDRNkey'=0);


//choose weight function hypothesis non deterministically; resolve for max P of success

endmodule





module reward_spawner


c : [0..delay] init 0; //reward spawn delay counter CANNOT be zero
t : [0..10] init 0;  //state counter to help ordering of events


despwnd : bool init false;
reward_present : bool init false;

[] (t=0) & waiting=false -> 1: (t' = 0);
[] (t=0) & waiting=true -> 1: (t' = 1);

[agent_move] (t=1) & reward_present=false & c<delay-> 1: (c'=c+1);  //increment reward spawn counter
[agent_move] (t=1) & reward_present=false & c>=delay -> (reward_present'=true) & (c'=0) & (t'=2); //spawn reward after "delay" steps
[agent_move] waiting=true & (t=2) & reward_present=true -> (t'=2);
[] waiting=false -> (t'=0) & (c'=0) & (reward_present'=false);

 
endmodule


formula HC = (pos=1?1:(pos=2?2:pos=3?3:0));
formula DRNtoOFC = (LH=1?3:(LH=0?1:1));
formula LH = (reward_collected=true?1:0);




//lookup tables for OFC to DRN
formula OFC1toDRN = (OFC1toDRNkey=0?ofc_to_drn_1:(OFC1toDRNkey=1?ofc_to_drn_2:(OFC1toDRNkey=2?ofc_to_drn_3:(OFC1toDRNkey=3?ofc_to_drn_4:(OFC1toDRNkey=4?ofc_to_drn_5:0)))));
formula OFC2toDRN = (OFC2toDRNkey=0?ofc_to_drn_1:(OFC2toDRNkey=1?ofc_to_drn_2:(OFC2toDRNkey=2?ofc_to_drn_3:(OFC2toDRNkey=3?ofc_to_drn_4:(OFC2toDRNkey=4?ofc_to_drn_5:0)))));
formula OFC3toDRN = (OFC3toDRNkey=0?ofc_to_drn_1:(OFC3toDRNkey=1?ofc_to_drn_2:(OFC3toDRNkey=2?ofc_to_drn_3:(OFC3toDRNkey=3?ofc_to_drn_4:(OFC3toDRNkey=4?ofc_to_drn_5:0)))));

//lookup tables for HC to OFC
formula HCtoOFC1 = HCtoOFC1key=0?hc_to_ofc_1:(HCtoOFC1key=1?hc_to_ofc_2:(HCtoOFC1key=2?hc_to_ofc_3:(HCtoOFC1key=3?hc_to_ofc_4:(HCtoOFC1key=4?hc_to_ofc_5:0))));
formula HCtoOFC2 = HCtoOFC2key=0?hc_to_ofc_1:(HCtoOFC2key=1?hc_to_ofc_2:(HCtoOFC2key=2?hc_to_ofc_3:(HCtoOFC2key=3?hc_to_ofc_4:(HCtoOFC2key=4?hc_to_ofc_5:0))));
formula HCtoOFC3 = HCtoOFC3key=0?hc_to_ofc_1:(HCtoOFC3key=1?hc_to_ofc_2:(HCtoOFC3key=2?hc_to_ofc_3:(HCtoOFC3key=3?hc_to_ofc_4:(HCtoOFC3key=4?hc_to_ofc_5:0))));

formula x = vis1 + vis2; //speed based on visual inputs



// reward_unseen_speed formula for each pf based on OFCxtoDRN connection strength

formula reward_unseen_speed_pf_1 = (OFC1toDRN = 10?reward_unseen_speed_1:(OFC1toDRN = 30?reward_unseen_speed_2:(OFC1toDRN = 90?reward_unseen_speed_3:(OFC1toDRN = 200?reward_unseen_speed_4:(OFC1toDRN = 250?reward_unseen_speed_5:0)))));
formula reward_unseen_speed_pf_2 = (OFC2toDRN = 10?reward_unseen_speed_1:(OFC2toDRN = 30?reward_unseen_speed_2:(OFC2toDRN = 90?reward_unseen_speed_3:(OFC2toDRN = 200?reward_unseen_speed_4:(OFC2toDRN = 250?reward_unseen_speed_5:0)))));
formula reward_unseen_speed_pf_3 = (OFC3toDRN = 10?reward_unseen_speed_1:(OFC3toDRN = 30?reward_unseen_speed_2:(OFC3toDRN = 90?reward_unseen_speed_3:(OFC3toDRN = 200?reward_unseen_speed_4:(OFC3toDRN = 250?reward_unseen_speed_5:0)))));


//todo get all speed values





